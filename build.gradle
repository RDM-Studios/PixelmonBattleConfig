buildscript {
    repositories {
        maven { url = "https://maven.minecraftforge.net/" }
	maven { url = "https://maven.parchmentmc.org" }
	maven { url = "https://repo.spongepowered.org/maven" }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
	classpath 'org.parchmentmc:librarian:1.+'
	classpath group: 'org.spongepowered', name: 'mixingradle', version: '0.7-SNAPSHOT'
    }
}

apply plugin: "net.minecraftforge.gradle"
apply plugin: "org.parchmentmc.librarian.forgegradle"
apply plugin: "org.spongepowered.mixin"
apply plugin: "eclipse"
apply plugin: "maven-publish"

version = "${mc_version}-${mod_version}"
group = project.group
archivesBaseName = project.modid

java.toolchain.languageVersion = JavaLanguageVersion.of 8

println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))
minecraft {
    mappings channel: project.mappings, version: project.mappings_version
    
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            workingDirectory project.file('run')

            arg "-mixin.config=${modid}.mixins.json"
	    property 'mixin.env.remapRefMap', 'true'
	    property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            mods {
                pixelmonbattleconfig {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')

            arg "-mixin.config=${modid}.mixins.json"
	    property 'mixin.env.remapRefMap', 'true'
	    property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            mods {
                pixelmonbattleconfig {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')

            arg "-mixin.config=${modid}.mixins.json"
	    property 'mixin.env.remapRefMap', 'true'
	    property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            args '--mod', 'pixelmonbattleconfig', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')

            mods {
                pixelmonbattleconfig {
                    source sourceSets.main
                }
            }
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
	maven {
		name = "CurseForge Maven" //Base
		url = "https://www.cursemaven.com"
	}
	maven {
		name = "Progwml6 Maven" //JEI
		url = "https://dvs1.progwml6.com/files/maven/"
	}
}

dependencies {
    // Base
    minecraft "net.minecraftforge:forge:${project.mc_version}-${project.forge_version}"
    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'

    // Libraries
    implementation fg.deobf("curse.maven:pixelmon-389487:${pixelmon_version}")
    
    // Extras
    implementation fg.deobf("mezz.jei:jei-${mc_version}:${jei_version}:api")
    implementation fg.deobf("mezz.jei:jei-${mc_version}:${jei_version}")
    implementation fg.deobf("curse.maven:just-enough-resources-jer-240630:${jer_version}")
    implementation fg.deobf("curse.maven:just-enough-effect-descriptions-jeed-532286:${jeed_version}")

    implementation fg.deobf("curse.maven:appleskin-248787:${appleskin_version}")

    implementation fg.deobf("curse.maven:the-one-probe-245211:${top_version}")

    implementation fg.deobf("curse.maven:configured-457570:${configured_version}")
    implementation fg.deobf("curse.maven:catalogue-459701:${catalogue_version}")
}

mixin {
	add sourceSets.main, "${modid}.refmap.json"
	config "${modid}.mixins.json"

	debug.export = true
}


jar {
    manifest {
        attributes([
            "Specification-Title": project.mod_name,
            "Specification-Vendor": "RDM Studios",
            "Specification-Version": project.mod_version,
            "Implementation-Title": project.mod_name,
            "Implementation-Version": project.mod_version,
            "Implementation-Vendor" :"RDM Studios",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
	    "MixinConfigs": "pixelmonbattleconfig.mixins.json"
        ])
    }
}

jar.finalizedBy('reobfJar') 

processResources {
	exclude '**/.cache/'

	duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file:///${project.projectDir}/mcmodsrepo"
        }
    }
}
